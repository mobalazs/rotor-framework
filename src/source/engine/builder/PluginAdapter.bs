namespace Rotor.ViewBuilder

    ' =====================================================================
    ' PluginHookClass - Container for plugin hook metadata and handler function
    ' =====================================================================
    class PluginHookClass

        pluginKey as string
        handlerFn as function

        ' ---------------------------------------------------------------------
        ' new - Initializes the instance
        '
        ' @description Initializes plugin hook with key and handler
        ' @param {object} config - Configuration with pluginKey and handlerFn
        ' ---------------------------------------------------------------------
        sub new(config)
            m.pluginKey = config.pluginKey
            m.handlerFn = config.handlerFn
        end sub

    end class

    ' =====================================================================
    ' pluginAdapter - Manages plugin registration and lifecycle hook integration. Maintains plugin instances and maps lifecycle hooks to plugin handlers.
    ' =====================================================================
    class pluginAdapter

        plugins as object

        ' ---------------------------------------------------------------------
        ' new - Initializes the instance
        '
        ' @description Initializes plugin hook storage for all lifecycle types
        ' ---------------------------------------------------------------------
        sub new()

            ' dedicated plugin lifecycle hooks
            for each hookType in [
                    Rotor.Const.LifeCycleHookType.BEFORE_MOUNT,
                    Rotor.Const.LifeCycleHookType.AFTER_MOUNTED,
                    Rotor.Const.LifeCycleHookType.BEFORE_UPDATE,
                    Rotor.Const.LifeCycleHookType.AFTER_UPDATED,
                    Rotor.Const.LifeCycleHookType.BEFORE_DESTROY
                ]
                m.pluginHooks[hookType] = {}
            end for

        end sub

        ' Plugins has limited type of hooks to lifecycle (check the list below)
        pluginHooks = {}
        pluginKeyList = CreateObject("roList")
        pluginInstanceHash = {} ' Maps plugin keys to plugin instances (supports multiple keys per plugin)

        frameworkInstance as Rotor.Framework

        ' ---------------------------------------------------------------------
        ' registerPlugins - Registers plugins and their lifecycle hooks with the framework
        '

        ' @param {object} pluginConfig - Plugin configuration object or array
        ' ---------------------------------------------------------------------
        sub registerPlugins (pluginConfig as object)

            plugins = Rotor.Utils.ensureArray(pluginConfig)

            for each plugin in plugins

                ' Plugin key
                pluginKey = plugin.pluginKey

                ' Setup hooks
                hooks = plugin.hooks
                if Rotor.Utils.isValid(hooks) and hooks.Count() > 0

                    for each hook in hooks
                        pluginHook = new PluginHookClass({
                            pluginKey: pluginKey,
                            handlerFn: hooks[hook]
                        })
                        m.pluginHooks[hook][pluginKey] = pluginHook
                    end for

                end if

                ' Register primary plugin key
                m.pluginKeyList.AddTail(pluginKey)

                ' Map all registry keys to the primary plugin key
                m.pluginInstanceHash[pluginKey] = pluginKey

                ' Add extra registry keys to pluginKeyList for widget detection
                if Rotor.Utils.isArray(plugin.aliasPluginKeys) and plugin.aliasPluginKeys.Count() > 0
                    for each aliasPluginKey in plugin.aliasPluginKeys
                        m.pluginKeyList.AddTail(aliasPluginKey)
                        m.pluginInstanceHash[aliasPluginKey] = pluginKey
                    end for
                end if

                ' Store plugin instance in framework plugins map
                m.frameworkInstance.plugins[pluginKey] = plugin

                ' Set framework instance reference
                plugin.frameworkInstance = m.frameworkInstance

                ' Initialize plugin if init method exists
                if Rotor.Utils.isFunction(plugin.init)
                    ' Initialize plugin
                    plugin.init()
                end if

            end for

        end sub

        ' ---------------------------------------------------------------------
        ' getPlugin - Retrieves plugin instance by key
        ' @param {string} pluginKey - Plugin registry key
        ' @returns {object} Plugin instance
        ' ---------------------------------------------------------------------
        public function getPlugin(pluginKey) as Rotor.BasePlugin
            primaryPluginKey = m.pluginInstanceHash[pluginKey]
            return m.frameworkInstance.plugins[primaryPluginKey]
        end function

        ' ---------------------------------------------------------------------
        ' destroyPlugins - Destroys all registered plugins
        ' ---------------------------------------------------------------------
        sub destroyPlugins()
            if m.plugins.Count() > 0
                for each plugin in m.plugins
                    plugin.frameworkInstance = invalid
                    destroy = plugin.destroy
                    if Rotor.Utils.isFunction(plugin.destroy)
                        destroy()
                    end if
                end for
            end if
        end sub

        ' ---------------------------------------------------------------------
        '

        ' init - Initializes the adapter with framework instance reference
        '
        ' @param {object} frameworkInstance - Framework instance reference
        ' ---------------------------------------------------------------------
        sub init(frameworkInstance as object)
            m.frameworkInstance = frameworkInstance
        end sub

        ' ---------------------------------------------------------------------
        ' destroy - Cleans up framework instance reference
        '
        ' ---------------------------------------------------------------------
        sub destroy()
            m.frameworkInstance = invalid
        end sub

    end class

end namespace

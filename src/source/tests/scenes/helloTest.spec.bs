import "pkg:/source/rotor/RotorFramework.bs"
import "pkg:/components/tests/helloTest/helloTest.template.bs"
import "pkg:/components/tests/helloTest/helloTest.const.bs"

namespace tests

    @SGNode("helloTest")
    @suite("HelloWorldRenderSuite integration")
    class HelloTestSuite extends rooibos.BaseTestSuite

        fwInstance as Rotor.Framework

        protected override function beforeEach()
            ' Initialize framework
            m.fwInstance = new Rotor.Framework()
            ' Render
            template = getHelloTemplate()
            m.fwInstance.render(template)
        end function

        protected override function afterEach()
            m.fwInstance.destroy()
            m.fwInstance = invalid
        end function

        @describe("Validate node tree after Rotor rotor has rendered template.")

        @it("creates expected nodes in native tree")
        function _()

            ' Get first node belong to helloTest template
            helloGroup = m.fwInstance.getRootWidget().children.helloGroup.node

            m.assertNotEqual(helloGroup, invalid)

            m.assertNotEqual(helloGroup, invalid)
            m.assertEqual(CDbl(helloGroup.translation[0]), CDbl(SAFE_AREA.x))
            m.assertEqual(CDbl(helloGroup.translation[1]), CDbl(SAFE_AREA.y))

            helloRect = helloGroup.getChild(0)

            m.assertNotEqual(helloRect, invalid)
            m.assertEqual(helloRect.subtype(), "Rectangle")
            m.assertEqual(helloRect.height, CDbl(HELLO_WORLD_CONFIG.RectHeight))
            m.assertEqual(helloRect.color, &h202020FF)
            m.assertEqual(helloRect.width, 0.0)

            helloLabel = helloRect.getChild(0)
            m.assertNotEqual(helloLabel, invalid)
            m.assertEqual(helloLabel.subtype(), "Label")
            m.assertEqual(helloLabel.text, HELLO_WORLD_TEXT)
            m.assertEqual(helloLabel.horizAlign, "center")
            m.assertEqual(helloLabel.vertAlign, "center")
            m.assertEqual(helloLabel.translation[0], CDbl(HELLO_WORLD_CONFIG.PaddingX))
            m.assertEqual(helloLabel.height, CDbl(HELLO_WORLD_CONFIG.RectHeight))
        end function

        @it("auto sizes nodes after render tracking fires")
        function _()

            ' Get widgets
            labelWidget = m.fwInstance.getWidget("helloLabel")
            m.assertNotEqual(labelWidget, invalid)
            rectWidget = m.fwInstance.getWidget("helloRect")
            m.assertNotEqual(rectWidget, invalid)

            ' Get nodes
            labelNode = labelWidget.node
            m.assertNotEqual(labelNode, invalid)
            rectNode = rectWidget.node
            m.assertNotEqual(rectNode, invalid)

            ' Note that template rendering works synchronously.
            ' Verify the effect of renderTracking observer callbacks(autoSizeObserver):
            originalRectNodeWidth = rectNode.width
            m.wait(90)
            ' rectNode width has been updated after Label text had been fully rendered.
            m.assertTrue(rectNode.width >= originalRectNodeWidth, "Rectangle width should cover label width")

        end function
    end class
end namespace
